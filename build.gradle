
plugins {
	id 'groovy'
	id 'java-gradle-plugin'
	id "com.gradle.plugin-publish" version "0.9.10"
}

allprojects {

	// Let's eat our own dog food (cannot be part of plugins block, though).
	apply plugin: ch.ergon.gradle.goodies.versioning.VersioningPlugin
	group = 'ch.ergon'

	repositories {
		jcenter()
	}

	test {
		maxParallelForks = Runtime.runtime.availableProcessors()
	}
}

test {
	inputs.files files("./versioning-test-data/")
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
	implementation gradleApi()
	implementation "org.codehaus.groovy:groovy-all:2.5.4"
	implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: jgitVersion

	testCompile "org.spockframework:spock-core:1.2-groovy-2.5"
}

gradlePlugin {
	plugins {
		versioningPlugin {
			id = 'ch.ergon.gradle.goodies.versioning'
			implementationClass = 'ch.ergon.gradle.goodies.versioning.VersioningPlugin'
		}
	}
}

pluginBundle {
	vcsUrl = 'https://github.com/kungfoo/ergon-gradle-goodies-versioning'
	description = 'Simple and automatic versioning from your git history for your java and android projects.'
	tags = ['versioning', 'git', 'java', 'android']

	plugins {
		versioningPlugin {
			displayName = 'Simple and automatic versioning from your git history for your java and android projects.'
			id = 'ch.ergon.gradle.goodies.versioning'
			website = 'https://github.com/kungfoo/ergon-gradle-goodies-versioning'
		}
	}
}
